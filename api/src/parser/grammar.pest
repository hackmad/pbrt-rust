pbrt = { SOI ~ stmt* ~ EOI }

stmt = { 
    empty_stmt | include_stmt | comment_stmt | option_stmt | scene_stmt
    | block_stmt | ctm_stmt
}

empty_stmt = _{ WHITESPACE_OR_NEWLINE+ }
comment_stmt = _{ WHITESPACE* ~ COMMENT }
include_stmt = { "Include" ~ quoted_str_expr }


block_stmt = {
    world_begin_stmt | world_end_stmt | attribute_begin_stmt | attribute_end_stmt
    | object_begin_stmt | object_end_stmt | transform_begin_stmt | transform_end_stmt
}

world_begin_stmt = { "WorldBegin" ~ STMT_END? }
world_end_stmt = { "WorldEnd" ~ STMT_END? }

attribute_begin_stmt = { "AttributeBegin" ~ STMT_END? }
attribute_end_stmt = { "AttributeEnd" ~ STMT_END? }

transform_begin_stmt = { "TransformBegin" ~ STMT_END? }
transform_end_stmt = { "TransformEnd" ~ STMT_END? }

object_begin_stmt = { "ObjectBegin" ~ quoted_str_expr_opt_end }
object_end_stmt = { "ObjectEnd" ~ STMT_END? }


option_stmt = {
    accelerator_stmt | camera_stmt | film_stmt | integrator_stmt
    | make_named_medium_stmt | sampler_stmt | pixel_filter_stmt
}

accelerator_stmt = { "Accelerator" ~ quoted_str_expr_opt_end ~ param_list? }
camera_stmt = { "Camera" ~ quoted_str_expr_opt_end ~ param_list? }
film_stmt = { "Film" ~ quoted_str_expr_opt_end ~ param_list? }
integrator_stmt = { "Integrator" ~ quoted_str_expr_opt_end ~ param_list? }
make_named_medium_stmt = { "MakeNamedMedium" ~ quoted_str_expr_opt_end ~ param_list? }
sampler_stmt = { "Sampler" ~ quoted_str_expr_opt_end ~ param_list? }
pixel_filter_stmt = { "PixelFilter" ~ quoted_str_expr_opt_end ~ param_list? }


scene_stmt = {
    area_light_source_stmt | light_source_stmt | make_named_material_stmt
    | material_stmt | shape_stmt | texture_stmt | named_material_stmt
    | object_instance_stmt | reverse_orientation_stmt | medium_interface_stmt 
    | active_transform_stmt
}

area_light_source_stmt = { "AreaLightSource" ~ quoted_str_expr_opt_end ~ param_list? }
light_source_stmt = { "LightSource" ~ quoted_str_expr_opt_end ~ param_list? }
make_named_material_stmt = { "MakeNamedMaterial" ~ quoted_str_expr_opt_end ~ param_list? }
material_stmt = { "Material" ~ quoted_str_expr_opt_end ~ param_list? }
shape_stmt = { "Shape" ~ quoted_str_expr_opt_end ~ param_list? }

texture_stmt = { "Texture" ~ quoted_str_expr_opt_end{3} ~ param_list? }

named_material_stmt = { "NamedMaterial" ~ quoted_ident_expr }
object_instance_stmt = { "ObjectInstance" ~ quoted_ident_expr }
reverse_orientation_stmt = { "ReverseOrientation" ~ STMT_END? }

medium_interface_stmt = { "MediumInterface" ~ quoted_str ~ (STMT_END? ~ quoted_str)? ~ STMT_END? }

active_transform_stmt = { "ActiveTransform" ~ active_transform_time ~ STMT_END? }
active_transform_time = { "StartTime" | "EndTime" | "All" }


ctm_stmt = {
    identity_stmt | translate_stmt | scale_stmt | rotate_stmt | look_at_stmt
    | coordinate_system_stmt | coord_sys_transform_stmt | transform_stmt
    | concat_transform_stmt | transform_times_stmt
}

identity_stmt = { "Identity" ~ STMT_END? }
translate_stmt = { "Translate" ~ float_expr{3} }
scale_stmt = { "Scale" ~ float_expr{3} }
rotate_stmt = { "Rotate" ~ float_expr{4} }
look_at_stmt = { "LookAt" ~ COMMENT? ~ float_expr{9} }
coordinate_system_stmt = { "CoordinateSystem" ~ quoted_ident_expr }
coord_sys_transform_stmt = { "CoordSysTransform" ~ quoted_ident_expr }
transform_stmt = { "Transform" ~ float_list_expr }
concat_transform_stmt = { "ConcatTransform" ~ float_list_expr }
transform_times_stmt = { "TransformTimes" ~ float ~ float_expr }


param_list = { (param | COMMENT)+ }
param = {
    point3d_param | vector3d_param | normal3d_param | point2d_param
    | vector2d_param | string_param | bool_param | float_param
    | int_param | colour_param | spectrum_param | blackbody_param
    | texture_param
}

point3d_param = ${
    QUOTATION_MARK ~ ("point3" | "point") ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ float_list_expr
}
vector3d_param = ${
    QUOTATION_MARK ~ ("vector3" | "vector") ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ float_list_expr
}
normal3d_param = ${
    QUOTATION_MARK ~ ("normal3" | "normal") ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ float_list_expr
}
point2d_param = ${
    QUOTATION_MARK ~ "point2" ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ float_list_expr
}
vector2d_param = ${
    QUOTATION_MARK ~ "vector2" ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ float_list_expr
}
string_param = ${
    QUOTATION_MARK ~ "string" ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ (quoted_str_expr | quoted_str_list_expr)
}
bool_param = ${
    QUOTATION_MARK ~ "bool" ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ (quoted_bool_expr | quoted_bool_list_expr)
}
float_param = ${
    QUOTATION_MARK ~ "float" ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ (float_expr | float_list_expr)
}
int_param = ${
    QUOTATION_MARK ~ "integer" ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ (int_expr | int_list_expr)
}
spectrum_param = ${
    QUOTATION_MARK ~ "spectrum" ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ (float_list_expr | quoted_str_expr)
}
blackbody_param = ${
    QUOTATION_MARK ~ "blackbody" ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ float_list_expr
}
texture_param = ${
    QUOTATION_MARK ~ "texture" ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ (quoted_str_expr | quoted_str_list_expr)
}
colour_param = ${
    QUOTATION_MARK ~ colour_type ~ WHITESPACE ~ ident ~ QUOTATION_MARK ~ WHITESPACE+
    ~ float_list_expr
}
colour_type = { "colour" | "color" | "rgb" | "xyz" }


float_list_expr = { "[" ~ WHITESPACE_OR_NEWLINE* ~ (float_expr ~ WHITESPACE_OR_NEWLINE*)+ ~ "]" ~ STMT_END? }
float_expr = { float ~ STMT_END? }
float = @{ float_no_int_before_period | float_int_before_period }
float_no_int_before_period = { ("+" | "-")? ~ "." ~ ASCII_DIGIT+ ~ (^"e" ~ int)?  }
float_int_before_period = { int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }


int_list_expr = { "[" ~ WHITESPACE_OR_NEWLINE* ~ (int_expr ~ WHITESPACE_OR_NEWLINE*)+ ~ "]" ~ STMT_END? }
int_expr = { int ~ STMT_END? }
int = { ("+" | "-")? ~ ASCII_DIGIT+ }


quoted_bool_list_expr = { "[" ~ WHITESPACE_OR_NEWLINE* ~ (quoted_bool_expr ~ WHITESPACE_OR_NEWLINE*)+ ~ "]" ~ STMT_END? }
quoted_bool_expr = { quoted_bool ~ STMT_END? }
quoted_bool = { QUOTATION_MARK ~ bool ~ QUOTATION_MARK }
bool = { "true" | "false" }


quoted_str_list_expr = { "[" ~ WHITESPACE_OR_NEWLINE* ~ (quoted_str_expr ~ WHITESPACE_OR_NEWLINE*)+ ~ "]" ~ STMT_END? }
quoted_str_expr_opt_end = { quoted_str ~ STMT_END? }
quoted_str_expr = { quoted_str ~ STMT_END? }
quoted_str = { QUOTATION_MARK ~ str ~ QUOTATION_MARK }
str = { (!QUOTATION_MARK ~ ANY)* }


quoted_ident_expr = { quoted_ident ~ STMT_END? }
quoted_ident = { QUOTATION_MARK ~ ident ~ QUOTATION_MARK }
ident_expr = { ident ~ STMT_END? }
ident = { LETTER ~ (ASCII_ALPHANUMERIC | "_" | "." | "-")* }


WHITESPACE = _{ " " | "\t" }
WHITESPACE_OR_NEWLINE = _{ WHITESPACE | NEWLINE }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }
STMT_END = _{ WHITESPACE* ~ COMMENT? ~ WHITESPACE_OR_NEWLINE+ }